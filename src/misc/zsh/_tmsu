#compdef tmsu

# Zsh completion script for tmsu. Copy this file to
# your Zsh function path, e.g. /usr/share/zsh/site-functions/_tmsu.

local context curcontext="$curcontext" state line
typeset -A opt_args

_tmsu() {
	local ret=1

	_arguments -C \
		'1: :_tmsu_commands' \
		'*::arg:->args' \
		&& ret=0

	case $state in
		(args)
			curcontext="${curcontext%:*:*}:tmsu-cmd-$words[1]:"
		 	case $line[1] in
				(delete)
					_arguments '*:tag:_tmsu_tags' && ret=0
				;;
				(dupes)
					_arguments '*:file:_files' && ret=0
				;;
				(export)
					# no args
				;;
				(files)
					_arguments '*:tag:_tmsu_tags' && ret=0
				;;
				(help)
					_arguments '1:command:_tmsu_commands' && ret=0
				;;
				(merge)
					_arguments '*:tag:_tmsu_tags' && ret=0
				;;
				(mount)
					_arguments '1:file:_files' \
						   '2:mountpoint:_files' \
					&& ret=0
				;;
				(rename)
					_arguments '1:tag:_tmsu_tags' && ret=0
				;;
				(stats)
					# no args
				;;
				(status)
					_arguments '*:file:_files' && ret=0
				;;
				(tag)
				;;
				(tags)
					_arguments '--list' \
					'*:file:_files' \
					&& ret=0
				;;
				(unmount)
					_arguments '1:mountpoint:_files' && ret=0
				;;
				(untag)
					#TODO
				;;
				(version)
					# no args
				;;
			esac
		;;
	esac
}

_tmsu_commands() {
	typeset -a command_list
	local line

	_call_program tmsu tmsu help --list | \
	while read -A line
	do
		command_list+=($line[1])
	done

	_describe -t commands 'command' command_list "$@"
}

_tmsu_tags() {
	typeset -a tag_list
	local tag

	_call_program tmsu tmsu tags --list | while read -A tag
	do
		tag_list+=$tag[1]
	done

	_describe -t tags 'tags' tag_list
}

_tmsu "$@"
